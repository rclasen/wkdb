#!/usr/bin/perl -w
#
# Copyright (c) 2008 Rainer Clasen
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms described in the file LICENSE included in this
# distribution.
#

# TODO: pod

use strict;
use warnings;
use Getopt::Long;
use Workout;
use Workout::Athlete;
use WkDB;

my $wanthelp;
my $needhelp;

if( ! GetOptions(
	"help|h!"	=> \$wanthelp,
)){
	$needhelp++;
}

if( $wanthelp ){
	print <<EOF;
$0 [opt] <fileid1> [<fileid2> ...]
submit workout data to endure database

Options:
--help                this cruft
EOF
	exit 0;
}

if( ! @ARGV ){
	print STDERR "missing input file ids\n";
	$needhelp++;
}

foreach my $id ( @ARGV ){
	if( $id !~ /^\d+$/  ){
		print STDERR "bad file id: $id\n";
		$needhelp++;
	}
}

if( $needhelp ){
	print STDERR "please use $0 --help for usage info\n";
	exit 1;
}


my $wkdb = WkDB->new;
my $db = $wkdb->db;

my %files;

foreach my $id ( @ARGV ) {
	my $row = $db->resultset('File')->find( $id )
		or die "no such file: $id";

	my $n = $row->pool->name;

	exists $files{$n}
		and die "multiple files of same type are not suppoted id=$id";

	$files{$n} = $row;
}

# TODO: automagically pick/merge the data from several files

# TODO: don't hardcode pool-specifc behavior

my $iter;
foreach my $n ( qw/ srm hrm gpx /, keys %files ){
	if( exists $files{$n} ){
		$iter = $files{$n}->workout->iterate;
		last;
	}
}

$iter 
	or die "failed to identify source workout";

my $si = Workout::filter( 'Info', $iter );
my $fi = Workout::filter( 'FTP', $si );
Workout::Store::Null->from( $fi );

my $sdate = DateTime->from_epoch( 
	epoch		=> $si->time_start,
	time_zone	=> 'local',
);

print 
"Anfang:               ", $sdate->strftime( '%F %H:%M:%S' ), "\n",
"Zeit Brutto:          ", fmtdur($si->dur), " h:m:s (", int($si->dur/60), "m)\n",
"Zeit Netto:           ", fmtdur($si->dur_mov), " h:m:s (", int($si->dur_mov/60), "m)\n",
"Temperatur:           ", (int($si->temp_avg||0) ||'?') ,  " °C\n",
"total work:           ", sprintf( "%.0f", ($si->work||0) / 1000 ), " kJ\n",
"Norm Power:           ", sprintf( "%.0f", ($fi->npwr||0) ), " W\n",
"Distanz:              ", sprintf( '%1.1f', ($si->dist||0) / 1000), " km\n",
"Geschwindigkeit, Max: ", sprintf( '%1.1f', ($si->spd_max||0) * 3.6), " km/h \n",
"Starthöhe:            ", (int($si->ele_start||0)||'?'), " m\n",
"Höhe, Min:            ", (int($si->ele_min||0)||'?'), " m\n",
"Höhe, Max             ", (int($si->ele_max||0)||'?'), " m\n",
"Gesamte Steigung:     ", (int($si->incline||0)||'?'), " m\n",
"Trittfrequenz, Avg:   ", (int($si->cad_avg||0)||'?'), " 1/min\n",
"Trittfrequenz, Max:   ", (int($si->cad_max||0)||'?'), " 1/min\n",
"Puls, Avg:            ", (int($si->hr_avg||0)||'?'), " 1/s\n",
"Puls, Max:            ", (int($si->hr_max||0)||'?'), " 1/s\n",
"--\n",
"Zeit creep:           ", fmtdur($si->dur_creep), " h:m:s (", 
	int(($si->dur_creep)/60), "m)\n",
"Zeit cad:             ", fmtdur($si->dur_cad), " h:m:s (",
	int(($si->dur_cad||0)/60), "m) ", 
	int(100*($si->dur_cad||0) / $si->dur_mov),"% \n",
"Zeit cad>0:           ", fmtdur( $si->dur_ncad ), " h (",
	int(($si->dur_ncad||0)/60), "m) (",
	sprintf('%.2f', ($si->cad_percent||0) ), "%)\n",
"avg speed:            ", sprintf( "%.2f", ($si->spd_avg||0) * 3.6 ), " km/h\n",
"avg Power:            ", sprintf( "%.2f", ($si->pwr_avg||0) ), " W\n",
"\n";


# TODO: actually submit data to DB / Web-API
$|=1;
print 'please enter exercise ID: ';
my $eid = <STDIN>;
if( $eid =~ /^(\d+)\s*$/ ){
	foreach my $row ( values %files ){
		$row->exercise( $eid );
		$row->ignore( 0 );
		$row->update;
	}
}

sub fmtdur {
	my $s = shift;
	my $m = int($s / 60); $s %= 60;
	my $h = int($m / 60); $m %= 60;
	sprintf('%d:%02d:%02d', $h, $m, $s);
}

sub max {
	my( $a, $b ) = @_;
	$a > $b ? $a : $b;
}

